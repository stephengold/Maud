// Gradle script to build the Maud project

plugins {
    id 'application' // to build JVM applications
    id 'checkstyle'  // to analyze Java sourcecode for style violations
}

ext {
    // current version of the JMonkeyEngine libraries:
    jme3Version = '3.7.0-beta1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.register('runForceDialog', JavaExec) {
    args '--forceDialog'
    mainClass = 'maud.Maud'
    description 'Runs the editor after displaying the Settings dialog.'
}

application {
    mainClass = 'maud.Maud'
}

checkstyle {
    toolVersion '10.16.0'
}

tasks.withType(JavaCompile).configureEach { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_20)) {
        // Suppress warnings that source value 8 is obsolete.
        options.compilerArgs << '-Xlint:-options'
    }
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX()

tasks.withType(JavaExec).configureEach { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    } else {
        //args '--forceDialog'
    }
    //args '--openGL3'
    //args '--openGL33'
    //args '--skipStartup'
    //args '--verbose' // to enable additional log output
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    jvmArgs '-Xms4g', '-Xmx4g' // to enlarge the Java heap
    //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of snapshots
}

dependencies {
    // from mavenCentral (or mavenLocal) repositories:
    implementation 'ch.qos.logback:logback-classic:1.5.0'
    runtimeOnly    'com.github.nifty-gui:nifty-style-black:1.4.3'
    implementation 'com.github.stephengold:Heart:9.0.0'
    implementation 'com.github.stephengold:Acorus:2.0.0'
    implementation 'com.github.stephengold:jme3-utilities-nifty:0.9.37'
    implementation 'com.github.stephengold:Minie:8.0.0'
    implementation 'com.github.stephengold:SkyControl:1.0.5'
    implementation 'com.github.stephengold:jme-ttf:3.0.1'
    implementation 'com.github.stephengold:Wes:0.8.1'
    runtimeOnly    'org.jmonkeyengine:jme3-awt-dialogs:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-blender:3.3.2-stable'
    runtimeOnly    'org.jmonkeyengine:jme3-desktop:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-lwjgl:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-plugins:' + jme3Version
    //runtimeOnly   'org.jmonkeyengine:jme3-testdata:3.1.0-stable'
    runtimeOnly    'org.openjdk.nashorn:nashorn-core:15.4'

    // from jitpack repositories:
    implementation 'com.github.xbuf.jme3_xbuf:jme3_xbuf_loader:0.9.1'
}

// Register cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSandbox', Delete) { // Acorus sandbox
    delete 'Written Assets'
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
