// Gradle build script for the Maud project

plugins {
    id 'application'
    id 'checkstyle'
}

ext.jme3Version = '3.6.0-beta3' // current version of JMonkeyEngine

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX()

tasks.withType(JavaExec) { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    } else {
        //args '--forceDialog'
    }
    //args '--openGL3'
    //args '--openGL33'
    //args '--skipStartup'
    //args '--verbose' // to enable additional log output
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xbatch'
    jvmArgs '-Xms4g', '-Xmx4g'
    //jvmArgs '-XX:+PrintCompilation'
    //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

application {
    mainClass = 'maud.Maud'
}

configurations.all {
   resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // to disable caching of SNAPSHOTs
}
repositories {
    //mavenLocal() // to find local SNAPSHOTs of libraries
    mavenCentral()
    maven { url 'https://jitpack.io' } // for jme3_xbuf_loader
    maven { url 'https://s01.oss.sonatype.org/content/groups/staging' } // to find libraries staged but not yet released
}

dependencies {
    // from mavenCentral (or mavenLocal) repositories:
    implementation 'ch.qos.logback:logback-classic:1.4.5'
    runtimeOnly    'com.github.nifty-gui:nifty-style-black:1.4.3'
    implementation 'com.github.stephengold:Heart:8.3.1+for36'
    implementation 'com.github.stephengold:Acorus:0.9.18+for36'
    implementation 'com.github.stephengold:jme3-utilities-nifty:0.9.34+for36'
    implementation 'com.github.stephengold:Minie:7.3.0+for36'
    implementation 'com.github.stephengold:SkyControl:1.0.3'
    implementation 'com.github.stephengold:jme-ttf:2.2.4'
    implementation 'com.github.stephengold:Wes:0.7.4+for36'
    runtimeOnly    'org.jmonkeyengine:jme3-awt-dialogs:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-blender:3.3.2-stable'
    runtimeOnly    'org.jmonkeyengine:jme3-desktop:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-lwjgl:' + jme3Version
    implementation 'org.jmonkeyengine:jme3-plugins:' + jme3Version
    //runtimeOnly   'org.jmonkeyengine:jme3-testdata:3.1.0-stable'
    implementation 'org.openjdk.nashorn:nashorn-core:15.4'

    // from jitpack repositories:
    implementation 'com.github.xbuf.jme3_xbuf:jme3_xbuf_loader:0.9.1'
}

tasks.register('runForceDialog', JavaExec) {
    args '--forceDialog'
    mainClass = 'maud.Maud'
}

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
tasks.register('cleanWrittenAssets', Delete) { // Acorus output directory
    delete 'Written Assets'
}
